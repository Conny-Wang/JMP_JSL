//!

// 新增 Apple 上传格式脚本

//================================================================

// 运行脚本前清除所有 JMP 变量名称和值
Clear Globals();
Clear Log();
Clear Symbols();
Delete Globals();
Delete Symbols();

//================================================================

// 创建一个窗体函数用来调用
NwFunc = Function({tips},
	New Window("警告",
		<<Modal,
		Text Box(tips),
		Button Box( "OK" ),
	)
);

//================================================================

// 选择文件夹
FilePath = Pick Directory( "选择追溯信息文件夹" );
If(FilePath == "",
	NwFunc("未选择文件夹，请重新选择！");
	Stop();
);

// 判断文件名
filelist = Files In Directory( FilePath );
filenames = {};
For( i = 1, i <= N Items( filelist ), i++,
	Insert Into( filenames, Right( filelist[i], 4 ) )
);
filenames = Associative Array( filenames ) << get keys;

If( N Items( filenames ) > 1 | filenames[1] != "xlsx",
	NwFunc( "文件格式不一致，只支持 xlsx 格式。" );
	Stop();
);

// 导入文件
mfi = Multiple File Import();
mfi << Set Folder( FilePath );
mfi << Set Charset( "gb2312" );
mfi << Set Add File Name Column( 0 );
mfi << Import Data();
// 判断表格式是否一致
TablesNum = N Table();
If( TablesNum > 1,
	NwFunc( "原始数据格式不一致，请检查！" );
	Stop();
);
// 设置为活动表
dt = Current Data Table();
// 改表名称
dt << Set Name( "Ship_Data" );
// 删除脚本列表
Script_List = dt << Get Table Script Names;
dt << Delete Scripts( Script_List );
// 判断原始数据是否正确
If( N Cols( dt ) != 17,
	NwFunc( "原始数据格式不正确，原始数据必须17列！" );
	Stop();
);
// 删除多余的列
dt << Delete Columns([4 14 16 17]);

// 新增 15 列用于存放数据
For( i = 1, i <= 16, i++,
	New Column( Char( i ), Character, "Nominal",Set Selected )
);
// 移动列
dt << Move Selected Columns( {:"1"n, :"2"n, :"3"n, :"4"n, :"5"n}, after( :BR_TOOL ) );
dt << Move Selected Columns( {:"6"n, :"7"n, :"8"n, :"9"n, :"10"n}, after( :SR_TOOL ) );
dt << Move Selected Columns( {:"11"n, :"12"n, :"13"n, :"14"n, :"15"n}, after( :SP_TOOL ) );
dt << Move Selected Columns( {:"16"n}, after( :JRS_ID ) );
// 截取字符串
dt:"1"n << Set Formula( Substr( :BR_TOOL, 8, 3 ) );
dt:"2"n << Set Formula( Substr( :BR_TOOL, 22, 1 ) );
dt:"3"n << Set Formula( Substr( :BR_TOOL, 15, 6 ) );

dt:"6"n << Set Formula( Substr( :SR_TOOL, 8, 3 ) );
dt:"7"n << Set Formula( Substr( :SR_TOOL, 22, 1 ) );
dt:"8"n << Set Formula( Substr( :SR_TOOL, 15, 6 ) );

dt:"11"n << Set Formula( Substr( :SP_TOOL, 8, 3 ) );
dt:"12"n << Set Formula( Substr( :SP_TOOL, 22, 1 ) );
dt:"13"n << Set Formula( Substr( :SP_TOOL, 15, 6 ) );

// 修改列名称
NewCols = {
"MB_SN", "ATTACH_DATE", "ATTACH_LINE", "ATTACH_FIXTURE", "BR_TRACE_ID", "BR_TOOL", "BR_BLOCK",
"BR_MOLDING_DC", "BR_MOLDING_MACHINE", "BR_SILICONE_TRACE_ID", "SR_TRACE_ID", "SR_TOOL",
"SR_BLOCK", "SR_MOLDING_DC", "SR_MOLDING_MACHINE", "SR_SILICONE_TRACE_ID", "SP_TRACE_ID",
"SP_TOOL", "SP_BLOCK", "SP_MOLDING_DC", "SP_MOLDING_MACHINE", "SP_SILICONE_TRACE_ID", "RACK",
"OVEN", "AOI_ID", "GNG_ID", "JRS_ID","PACK_ID", "GLUE_BATCH"
};
	
For( 
	i = 1, i <= N Items( NewCols ), i++,
	Column( dt, i ) << set name( NewCols[i] )
);

// 修改时间列
dt:ATTACH_DATE << Format( "m/d/y h:m", 23 );

// 保存数据
dt << Save( "$Desktop/" || "Ship_Data_" || Char( Today() ) || ".csv" );

Close(dt,No Save);