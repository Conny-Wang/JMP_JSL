//!

// 2024/09/02 新增 XHD 全模脚本

//================================================================

// 运行脚本前清除所有 JMP 变量名称和值
Clear Globals();
Clear Log();
Clear Symbols();
Delete Globals();
Delete Symbols();

//================================================================

// 创建一个窗体函数用来调用
NwFunc = Function({tips},
	New Window("警告",
		<<Modal,
		Text Box(tips),
		Button Box( "OK" ),
	)
);

//================================================================

// 选择文件夹
FilePath = Pick Directory( "Select a directory" );
If(FilePath == "",
	NwFunc("未选择文件夹，请重新选择！");
	Stop();
);
// 导入文件
mfi = Multiple File Import();
mfi << Set Folder( FilePath );
mfi << Set Charset( "gb2312" );
mfi << Set Add File Name Column( 1 );
mfi << Import Data();
// 判断表格式是否一致
TablesNum = N Table();
If( TablesNum > 1,
	NwFunc( "原始数据格式不一致，请检查！" );
	Stop();
);
// 设置为活动表
dt = Current Data Table();
// 改表名称
dt << Set Name( "XHD_Data" );
// 删除脚本列表
Script_List = dt << Get Table Script Names;
dt << Delete Scripts( Script_List );
// 删除多余的行
col1 = Column( 1 );
col2 = Column( 2 );
dt << Select Where( dt:col1 == "SN" );
dt << Delete Rows;
dt << Select Where( dt:col2 == "" );
dt << Delete Rows;
// 删除前三列
dt << Delete Columns(1,2,3);

//修改列名称
NewCols = {
	"F1_g", "F1 mm", "F2_g", "F2 mm", "F3_g", "F3 mm",
	"F4_g", "F4 mm", "Cf/Cr_g", "Cf/Cr_%", "Max","Max mm"
};

For( 
	i = 1, i <= N Items( NewCols ), i++,
	Column( dt, i ) << set name( NewCols[i] )
);

// 去除 * 号
// 替换数据表中的全部
Local( {dt = Data Table( "XHD_Data" )},
	dt << Begin Data Update;
	For Each( {col},
		dt << Get Column Reference( dt << Get Column Names( Character ) ),
		For Each Row( dt, col[] = Substitute( col[], "*", " ", <<IGNORECASE ) )
	);
	dt << End Data Update;
);


// 所有转换为数字
For( 
	i = 1, i <= N Items( NewCols ), i++,
	cols = column(i);
	dt:cols << Data Type( Numeric ) << Set Modeling Type( "Continuous" );
);


dt << Graph Builder(
	Size( 1155, 660 ),
	Variables( Y( :F1_g ), Y( :F4_g ), Y( :"Cf/Cr_%"n ), Group X( :File Name ) ),
	Elements( Position( 1, 1 ), Points( Y, Legend( 13 ) ) ),
	Elements( Position( 1, 2 ), Points( Y, Legend( 14 ) ) ),
	Elements( Position( 1, 3 ), Points( Y, Legend( 15 ) ) ),
	SendToReport(
		Dispatch( {}, "graph title", TextEditBox, {Set Text( "XHD_Force" )} )
	)
);

//保存数据表
//dt << Save As("$Desktop\SE_Data.jmp");

